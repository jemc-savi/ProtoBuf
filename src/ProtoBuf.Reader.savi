:class ProtoBuf.Reader
  :var _depth USize: 0
  :let _wire _WireReader
  :new (stream)
    @_wire = _WireReader.new(stream)

  :fun ref bool! Bool: @_wire.read_varint_as_u64! != 0

  :fun ref uint32! U32: @_wire.read_varint_as_u64!.u32
  :fun ref uint64! U64: @_wire.read_varint_as_u64!
  :fun ref int32!  I32: @_wire.read_varint_as_u64!.i32
  :fun ref int64!  I64: @_wire.read_varint_as_u64!.i64
  :fun ref sint32! I32: _ZigZag.decode_32(@_wire.read_varint_as_u64!.u32)
  :fun ref sint64! I64: _ZigZag.decode_64(@_wire.read_varint_as_u64!)

  :fun ref fixed32!  U32: @_wire.stream.take_native_u32!
  :fun ref fixed64!  U64: @_wire.stream.take_native_u64!
  :fun ref sfixed32! I32: @_wire.stream.take_native_u32!.i32
  :fun ref sfixed64! I64: @_wire.stream.take_native_u64!.i64
  :fun ref float!    F32: F32.from_bits(@_wire.stream.take_native_u32!)
  :fun ref double!   F64: F64.from_bits(@_wire.stream.take_native_u64!)

  :fun ref bytes! Bytes
    size = @_wire.read_varint_as_u64!.usize!
    error! if (@_wire.stream.bytes_ahead < size)

    @_wire.stream
      .mark_here
      .advance!(size)
      .extract_token

  :fun ref string! String
    size = @_wire.read_varint_as_u64!.usize!
    error! if (@_wire.stream.bytes_ahead < size)

    @_wire.stream
      .mark_here
      .advance!(size)
      .extract_token.as_string

  :fun ref unknown_field(key U64)
    // TODO: Track an "unknown field" warning object here for possible printing
    // (because some users may want to know about a protocol version mismatch).

    // The kind of field is indicated by the lowest three bits of the key.
    kind = key.u8.bit_and(0b111)

    // Skip the field's value, based on knowing the kind of encoding it uses.
    try (
      case kind == (
      | 0 | @_wire.skip_varint!
      | 1 | @_wire.skip_64_bit_value!
      | 2 | @_wire.skip_sized_value!
      // TODO: 3: "start group"
      // TODO: 4: "end group"
      | 5 | @_wire.skip_32_bit_value!
      |
        // TODO: Track an "invalid key" error object here for printing.
        error!
      )
    )

    @

  :fun ref each_in_object!
    @_depth += 1
    if (@_depth == 1) (
      @each_in_top_object! -> (key | yield key)
      @_depth -= 1
      return
    )

    size = @_wire.read_varint_as_u64!.usize!
    finish_line = size + @_wire.stream.bytes_behind
    while (@_wire.stream.bytes_behind < finish_line) (
      yield @_wire.read_varint_as_u64!
    )

    @_depth -= 1
    @

  :fun ref each_in_top_object!
    while @_wire.has_bytes_ahead (
      yield @_wire.read_varint_as_u64!
    )
    @

// TODO: Packed encoding of repeated numeric fields: https://developers.google.com/protocol-buffers/docs/encoding#packed
